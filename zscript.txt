version "2.5"

//------------------------------------------------------------
//
// Base weapon class and functions
//
//------------------------------------------------------------
class TQ_Weapon : Weapon {
	//Credit to SanyaWaffles via Discord for the idea of these functions
	action bool TQ_CheckButtonPress(int btn) {
		return ((GetPlayerInput(INPUT_BUTTONS) & btn) && !(GetPlayerInput(INPUT_OLDBUTTONS) & btn));
	}

	action bool TQ_CheckButtonHold(int btn) {
		return (GetPlayerInput(INPUT_BUTTONS) & btn);
	}

	action bool TQ_CheckButtonRelease(int btn) {
		return ((GetPlayerInput(INPUT_OLDBUTTONS) & btn) && !(GetPlayerInput(INPUT_BUTTONS) & btn));
	}
}

struct RoundData {
	bool Live; //Is the round fire-able
}

struct ChamberData {
	RoundData Round;
	bool Loaded; //Is there a round in the chamber
}

struct SlideData {
	double Pos; //Where the slide is. 0=forward, 1=fully back
	double Spring; //How much the slide tries to move forward every tic
	double Pressure; //How much back-pressure is currently applied to the slide
	bool Locked; //Is the slide lock engaged
}

struct MagData {
	int MaxRounds; //Maximum number of rounds in magazine
	int Rounds; //Current rounds in magazine
}

//------------------------------------------------------------
//
// Pistol Weapon
//
//------------------------------------------------------------
class TQ_Pistol : TQ_Weapon {

	ChamberData Chamber; //Initialze chamber
	SlideData Slide; //Initialize slide
	MagData Mags[4]; //Initialize magazine array
	int MagIndex; //Current magazine. Index 0 is no mag loaded
	bool SafetyOn; //Is the safety engaged

	Default	{
		Weapon.SlotNumber 2;
		Weapon.AmmoType "Clip"; //Temporary tracking for rounds in mag
		Weapon.AmmoType2 "RocketAmmo"; //temporary tracking for round in chamber
	}

	override void PostBeginPlay() {
		//Set up slide values, randomize some
		Slide.Pos = 0.0;
		Slide.Spring = 0.5;
		Slide.Pressure = 0.0;
		Slide.Locked = false;
		if(random(0,1) == 1) {
			Slide.Pos = 1.0;
			Slide.Locked = true;
		}

		//Set up chamber values, randomize some
		Chamber.Loaded = false;
		if(random(0,1) == 1) {
			Chamber.Loaded = true;
			Chamber.Round.Live = true;
		}

		//Set up magazines, randomize some
		MagIndex = random(1,3);
		for(int i = 1;i <= 3;i++) {
			Mags[i].MaxRounds = 7;
			Mags[i].Rounds = random(0,Mags[i].MaxRounds);
		}
		super.PostBeginPlay();
	}

	override void Tick() {
		//Bookkeeping: if no round in chamber, it can't be live either
		if(!Chamber.Loaded) {
			Chamber.Round.Live=false;
		}

		//Slide routines that happen automatically
		If((Slide.Locked) & (Slide.Pos >= 0.8)) {
			Slide.Pos += (Slide.Pressure - Slide.Spring); //Spring pushes slide forward
			Slide.Pos = clamp(Slide.Pos, 0.8, 1); //Keep slide back if lock engaged and slide is behind it
		}
		else {
			Slide.Pos += (Slide.Pressure - Slide.Spring); //Spring pushes slide forward
			Slide.Pos = clamp(Slide.Pos, 0, 1); //Allow full travel otherwise
		}

		if((!SlideMovingBack()) & (Slide.Pos < 0.8) & (!CheckSlideForward())) {
			LoadRoundFromMag(); //Always attempt to load a round if slide going forward and past lock
		}

		if((SlideMovingBack()) & (Slide.Pos > 0.95)) {
			Slide.Locked = false; //Disengage slide lock,will auto-engage if needed
			EjectRound(); //Always attempt to load a round if slide going back and past 90%
		}

		if(Mags[MagIndex].Rounds == 0 & MagIndex != 0) {
			Slide.Locked = true;
		}

		super.Tick();
    }

	States {
		Spawn:
			PISG P -1;
			Loop;
		Select:
			TNT1 A 0 TQ_InitializeOverlays;
		SelectLoop:
			TNT1 A 1 A_Raise;
			Loop;
		Deselect:
			TNT1 A 1 A_Lower;
			Loop;
		Fire:
			//Placeholder
		Ready:
			//Intentional fallthrough to Primary context

		//Primary Weapon Action States
		Primary.Ready:
			TNT1 A 1 TQ_PrimaryInput;
			Loop;
		Primary.Fire:
			TNT1 A 1 TQ_PullTrigger;
			TNT1 A 1 { invoker.slide.pressure = 0; }
			Goto Primary.Ready;
		Primary.Reload:
			TNT1 A 5  MagRelease();
			TNT1 A 15 MagicReload();
			TNT1 A 5  MagInsert();
			Goto Primary.Ready;

		//Alt weapon action States
		Alt.Ready:
			TNT1 A 1 TQ_AltInput;
			Loop;
		Alt.ReleaseLock:
			TNT1 A 1 TQ_ReleaseSlideLock;
			Goto Alt.Ready;
		Alt.Pullslide:
			TNT1 A 1 TQ_PullSlide;
			Goto Alt.Ready;

		//Overlays
		MagOverlay:
			9MAG A 1;
			Loop;
		FrameOverlay:
			9FRM A 1;
			Loop;
		SlideOverlay:
			9SLD A 1 A_OverlayOffset(4,-16.0 * invoker.Slide.Pos,0,0);
			TNT1 A 0 {
				//Debugging
				A_SetInventory("Clip",invoker.Mags[invoker.MagIndex].Rounds);
				if(invoker.Chamber.Loaded) {
					SetInventory("RocketAmmo",1);
				}
				else {
					SetInventory("RocketAmmo",0);
				}
			}
			Loop;
	}

	action void TQ_InitializeOverlays() {
		A_SetCrosshair(99); //Set null crosshair (assuming player doesn't override it)
		A_Overlay(2,"MagOverlay");
		A_Overlay(3,"FrameOverlay");
		A_Overlay(4,"SlideOverlay");
		A_OverlayFlags(2,PSPF_ADDBOB,0);
		A_OverlayFlags(3,PSPF_ADDBOB,0);
		A_OverlayFlags(4,PSPF_ADDBOB,0);
	}

	action state TQ_PrimaryInput() {
		//A_Print("Primary");
		if(TQ_CheckButtonPress(BT_ATTACK)) {
			A_Print("Primary > pull trigger");
			return ResolveState("Primary.Fire");
		}
		if(TQ_CheckButtonPress(BT_RELOAD)) {
			A_Print("Primary > reload");
			return ResolveState("Primary.Reload");
		}
		if(TQ_CheckButtonHold(BT_ALTATTACK)) {
			A_Print("Primary > Alt");
			return ResolveState("Alt.Ready");
		}
		A_WeaponReady(WRF_NOFIRE|WRF_NOPRIMARY|WRF_NOSECONDARY);
		return ResolveState(null);
	}

	action state TQ_AltInput() {
		//A_Print("Alt");
		if(TQ_CheckButtonPress(BT_ATTACK)) {
			A_Print("Alt > Release slide lock");
			return ResolveState("Alt.ReleaseLock");
		}
		if(TQ_CheckButtonHold(BT_RELOAD)) {
			A_Print("Alt > pulling");
			return ResolveState("Alt.PullSlide");
		}
		if(TQ_CheckButtonRelease(BT_RELOAD)) {
			invoker.Slide.Pressure = 0;
			A_Print("Alt > done pulling");
		}
		if(!TQ_CheckButtonHold(BT_ALTATTACK)) {
			A_Print("Alt > Primary");
			return ResolveState("Primary.Ready");
		}
		A_WeaponReady(WRF_NOFIRE|WRF_NOPRIMARY|WRF_NOSECONDARY);
		return ResolveState(null);
	}

	action void TQ_ReleaseSlideLock() {
		invoker.Slide.Locked = false;
	}

	action void TQ_PullSlide() {
		invoker.Slide.Pressure += 0.2; //Increase back-pressure as held
		clamp(invoker.Slide.Pressure, 0, 2); //Maximum pressure exerted
	}

	action void TQ_PullTrigger() {
		if(CheckSlideForward() && invoker.Chamber.Round.Live) {
			invoker.Slide.Pressure = 10;
			invoker.Chamber.Round.Live = false;
			A_PlaySound("weapons/pistol",CHAN_AUTO);
		}
	}

	action void EjectRound() {
		if(invoker.Chamber.Loaded) {
			invoker.Chamber.Loaded=false;
		}
	}

	action void LoadRoundFromMag() {
		if(!invoker.Chamber.Loaded && invoker.Mags[invoker.MagIndex].Rounds > 0) {
			invoker.Chamber.Loaded = true;
			invoker.Chamber.Round.Live = true;
			invoker.Mags[invoker.MagIndex].Rounds -= 1;
		}
	}

	action void MagRelease() {
		A_PlaySound("TQMagDrop",CHAN_AUTO);
		//invoker.MagIn=false;
	}

	action void MagicReload() {
		invoker.Mags[invoker.MagIndex].Rounds = 7;
	}

	action void MagInsert() {
		A_PlaySound("TQMagIn",CHAN_AUTO);
		//invoker.MagIn=true;
	}

	action bool CheckSlideForward() {
		return (invoker.Slide.Pos == 0); //True if pos at zero
	}

	action bool SlideMovingBack() {
		if ((invoker.Slide.Pressure - invoker.Slide.Spring > 0)) {
			return true;
		}
		return false;
	}
}

//------------------------------------------------------------
//
// Player Definition
//
//------------------------------------------------------------
class TQ_Player : Doomplayer
{
	Default {
		Player.StartItem "TQ_Pistol";
		Player.StartItem "Fist";
		Player.Viewheight 50;
		Player.AttackZOffset 26;
		Player.ForwardMove 1.0;
		Player.SideMove 0.8;
		Player.RunHealth 35;
		Player.JumpZ 5.0;
	}
}