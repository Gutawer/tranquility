//------------------------------------------------------------
//
// Basic 9mm pistol
//
//------------------------------------------------------------

Actor TQ_9mm_Round : TQ_GenericAmmo
{
	Inventory.PickupMessage "Picked up a round of 9mm"
	Inventory.Amount 1
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 40
	Ammo.BackpackMaxAmount 400
	Inventory.Icon "CLIPA0"
	States
	{
	Spawn:
		CLIP A -1
		Stop
	}
}

Actor TQ_9mm_Bullet : TQ_GenericBullet
{
	Speed 600
	Damage (random(310,415)*random(1,3)/50) // Generic 9mm, ~362 ft.lbf +/-15%
	States
	{
	Death:
		TNT1 A 0
		PUFF AB random(1,2)
		TNT1 AAA 0 A_SpawnItemEx("TQ_BulletPuff",0,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3))
		Stop
	}
}

ACTOR TQ_9mm_Pistol : TQ_GenericWeapon
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "TQ_9mm_Round"
	Weapon.SlotNumber 2
	States
	{
	Spawn:
		PIST A -1
		Stop
	Ready:
		PISG A 1 A_WeaponReady
		Loop
	Deselect:
		PISG A 1
		{
			A_Lower;
			A_TakeInventory("TQ_AimDownSights");
			A_SetCrosshair(0);
		}
		Loop
	Select:
		PISG A 1
		{
			A_Raise;
			A_TakeInventory("TQ_AimDownSights");
			A_SetCrosshair(98);
		}
		Loop
	Fire:
		PISG A 4
		PISG B 6 
		{ 
			A_FireCustomMissile("TQ_9mm_Bullet",CAllACS("GetOffset",0)/(180),1,0,0,0,CAllACS("GetOffset",1)/(90));
			//A_FireBullets(CAllACS("GetOffset",0)/(180),CAllACS("GetOffset",1)/(-90),-1,5,"BulletPuff",FBF_EXPLICITANGLE);
		}
		PISG C 4
		PISG B 5 A_ReFire
		Goto Ready
	}
}