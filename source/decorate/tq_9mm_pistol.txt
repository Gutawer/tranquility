//------------------------------------------------------------
//
// Basic 9mm pistol
//
//------------------------------------------------------------

ACTOR TQ_9mm_Pistol : Weapon
{
	const int RecoilStrength = 4;
	Weapon.KickBack 100
	Weapon.BobRangeX 0.65
	Weapon.BobRangeY 0.5
	Weapon.BobSpeed 2.5
	Weapon.BobStyle InverseSmooth
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "TQ_9mm_Round"
	Weapon.SlotNumber 2
	+WEAPON.NOAUTOFIRE
	+WEAPON.NO_AUTO_SWITCH
	States
	{
	Spawn:
		PIST A -1
		Stop
	
	Ready:
		PISG A 1
		{
			A_WeaponReady;
			A_WeaponOffset(CAllACS("GetOffset",2)*(-0.25),CAllACS("GetOffset",3)*(-0.2),WOF_ADD|WOF_ADD);
		}
		Loop
	
	Deselect:
		PISG A 1
		{
			A_Lower;
			A_SetCrosshair(0);
			A_Overlay(2,"");
			A_Overlay(3,"");
		}
		Loop
	
	Select:
		PISG A 1
		{
			A_Raise;
			A_SetCrosshair(99);
			A_Overlay(2,"FSight");
			A_Overlay(3,"RSight");
			A_OverlayFlags(2,PSPF_ADDBOB,0);
			A_OverlayFlags(3,PSPF_ADDBOB,0);
		}
		Loop
		
	Fire:
		PISG A 1
		{
			A_GunFlash;
			A_PlaySound("weapons/pistol", CHAN_WEAPON);
			A_FireCustomMissile("TQ_9mm_Bullet",CAllACS("GetOffset",2)*(1),1,0,0,0,CAllACS("GetOffset",3)*(1));
		}
		PISG B 2 { CAllACS("TQ_RecoilOffset",RecoilStrength); }
		PISG CD 1 A_WeaponReady
		PISG CCCA 1 
		{
			A_WeaponReady;
			If(!(GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK))
			{
				return state("Ready");
			}
			CAllACS("TQ_RecoilOffset",(RecoilStrength/4)*(-1));
			return state("");
		}
		Goto Ready
		
	Flash:
		PISG F random(0,1)
		Stop
	
	FSight:
		TNT1 A 0
		{
			A_OverlayOffset(2,CAllACS("GetOffset",2)*(-1),CAllACS("GetOffset",3)*(-1));
			if ((CAllACS("GetOffset",2) > 15 || CAllACS("GetOffset",2) < -15 || CAllACS("GetOffset",3) > 15 || CAllACS("GetOffset",3) < -15))
			{ return state("HideFSight"); }
			Return state("");
		}
		SIGT A 1
		loop
	HideFSight:
		TNT1 A 1
		Goto FSight
	
	RSight:
		TNT1 A 0
		{
			A_OverlayOffset(3,CAllACS("GetOffset",2)*(-0.5),CAllACS("GetOffset",3)*(-0.5));
			if ((CAllACS("GetOffset",2) > 15 || CAllACS("GetOffset",2) < -15 || CAllACS("GetOffset",3) > 15 || CAllACS("GetOffset",3) < -15))
			{ return state("HideRSight"); }
			Return state("");
		}
		SIGT B 1
		loop
	HideRSight:
		TNT1 A 1
		Goto RSight
	}
}