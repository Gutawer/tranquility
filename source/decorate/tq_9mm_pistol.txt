//------------------------------------------------------------
//
// Basic 9mm pistol
//
//------------------------------------------------------------

Actor TQ_9mm_Round : Ammo
{
	Inventory.PickupMessage "Picked up a round of 9mm"
	Inventory.Amount 1
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 40
	Ammo.BackpackMaxAmount 400
	Inventory.Icon "CLIPA0"
	States
	{
	Spawn:
		CLIP A -1
		Stop
	}
}

Actor TQ_9mm_Bullet : TQ_GenericBullet
{
	Speed 600
	Damage (random(310,415)*random(1,3)/50) // Generic 9mm, ~362 ft.lbf +/-15%
	States
	{
	Death:
		TNT1 A 0
		PUFF AB random(1,2)
		TNT1 AAA 0 A_SpawnItemEx("TQ_BulletPuff",0,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3))
		Stop
	}
}

ACTOR TQ_9mm_Pistol : Weapon
{
	const int RecoilStrength = 4;
	Weapon.KickBack 100
	Weapon.BobRangeX 0.65
	Weapon.BobRangeY 0.5
	Weapon.BobSpeed 2.5
	Weapon.BobStyle InverseSmooth
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "TQ_9mm_Round"
	Weapon.SlotNumber 2
	+WEAPON.NOAUTOFIRE
	+WEAPON.NO_AUTO_SWITCH
	States
	{
	Spawn:
		PIST A -1
		Stop
	
	Ready:
		PISG A 1
		{
			A_WeaponReady;
			A_WeaponOffset(CAllACS("GetOffset",2)*(-0.25),CAllACS("GetOffset",3)*(-0.2),WOF_ADD|WOF_ADD);
		}
		Loop
	
	Deselect:
		PISG A 1
		{
			A_Lower;
			A_TakeInventory("TQ_AimDownSights");
			A_SetCrosshair(0);
		}
		Loop
	
	Select:
		PISG A 1
		{
			A_Raise;
			A_TakeInventory("TQ_AimDownSights");
			A_SetCrosshair(98);
		}
		Loop
		
	Fire:
		PISG A 1
		{
			A_PlaySound("weapons/pistol", CHAN_WEAPON);
			A_FireCustomMissile("TQ_9mm_Bullet",CAllACS("GetOffset",2)*(1),1,0,0,0,CAllACS("GetOffset",3)*(1));
		}
		PISG B 2 { CAllACS("TQ_RecoilOffset",RecoilStrength); }
		PISG CD 1 A_WeaponReady
		PISG CCCA 1 
		{
			A_WeaponReady;
			If(!(GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK))
			{
				return state("Ready");
			}
			CAllACS("TQ_RecoilOffset",RecoilStrength/4*(-1));
			return state("");
		}
		Goto Ready
	
	AltFire:
		"####" "#" 5
		{
			if(CountInv("TQ_AimDownSights") > 0)
			{
				A_TakeInventory("TQ_AimDownSights");
				A_SetCrosshair(98);
			}
			else
			{
				A_GiveInventory("TQ_AimDownSights");
				A_Overlay(2,"FSight");
				A_Overlay(3,"RSight");
				A_OverlayFlags(2,PSPF_ADDBOB,0);
				A_OverlayFlags(3,PSPF_ADDBOB,0);
				A_SetCrosshair(99);
			}
			return state("Ready");
		}
	
	FSight:
		SIGT A 1
		"####" "#" 1
		{
			A_OverlayOffset(2,CAllACS("GetOffset",2)*(-1),CAllACS("GetOffset",3)*(-1));
			if(CountInv("TQ_AimDownSights") > 0)
			{
				return state("FSight");
			}
			else
			{
				return state("StopSights");
			}
		}
		Stop
	
	RSight:
		SIGT B 1
		"####" "#" 1
		{
			A_OverlayOffset(3,CAllACS("GetOffset",2)*(-0.5),CAllACS("GetOffset",3)*(-0.5));
			if(CountInv("TQ_AimDownSights") > 0)
			{
				return state("RSight");
			}
			else
			{
				return state("StopSights");
			}
		}
		Stop
	
	StopSights:
		TNT1 A 1
		Stop
	}
}