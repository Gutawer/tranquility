//------------------------------------------------------------
//
// Basic 9mm pistol
//
//------------------------------------------------------------

//------------------------------------------------------------
// Housekeeping Items
//------------------------------------------------------------
Actor TQ_9mm_Pistol_RoundInChamber : Ammo
{
	Inventory.MaxAmount 1
	+IGNORESKILL
}

Actor TQ_9mm_Pistol_RoundsInMag : Ammo
{
	Inventory.MaxAmount 15
	+IGNORESKILL
}

//------------------------------------------------------------
// Actual Weapon
//------------------------------------------------------------
ACTOR TQ_9mm_Pistol : Weapon
{
	const int RecoilStrength = 8;
	Weapon.KickBack 100
	Weapon.BobRangeX 0.65
	Weapon.BobRangeY 0.5
	Weapon.BobSpeed 2.5
	Weapon.BobStyle InverseSmooth
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "TQ_9mm_Round"
	Weapon.SlotNumber 2
	+WEAPON.NOAUTOFIRE
	+WEAPON.NO_AUTO_SWITCH
	States
	{
	Spawn:
		PISG P -1
		Stop
	
	Ready:
		PISG A 1
		{
			A_GiveInventory("TQ_9mm_Pistol_RoundInChamber");
			A_GiveInventory("TQ_9mm_Pistol_RoundsInMag",15);
		}
	ReadyLoop:
		"####" "#" 1
		{
			A_WeaponReady;
			A_WeaponOffset(CAllACS("GetOffset",2)*(-0.25),CAllACS("GetOffset",3)*(-0.2),WOF_ADD|WOF_ADD);
		}
		Loop
	
	Deselect:
		PISG A 1
		{
			A_Lower;
			A_SetCrosshair(0);
			A_Overlay(2,"");
			A_Overlay(3,"");
		}
		Loop
	
	Select:
		PISG A 1
		{
			A_Raise;
			A_SetCrosshair(99);
			A_Overlay(2,"FSight");
			A_Overlay(3,"RSight");
			A_OverlayFlags(2,PSPF_ADDBOB,0);
			A_OverlayFlags(3,PSPF_ADDBOB,0);
		}
		Loop
		
	Fire:
		"####" "#" 1
		{
			If(countinv("TQ_9mm_Pistol_RoundInChamber")==1)
			{
				A_GunFlash;
				A_PlaySound("weapons/pistol", CHAN_WEAPON);
				A_FireCustomMissile("TQ_9mm_Bullet",CAllACS("GetOffset",2)*(0.75),0,0,0,0,CAllACS("GetOffset",3)*(0.75));
				A_TakeInventory("TQ_9mm_Pistol_RoundInChamber");
				return state("SlideBack");
			}
			Return state("");
		}
	DryFire:
		"####" "#" 2 Offset(0,33)
		"####" "#" 2 Offset(0,32)
		Goto ReadyLoop
	SlideBack:	
		PISG B 1
		{
			CAllACS("TQ_RecoilOffset",RecoilStrength);
			If (CountInv("TQ_9mm_Pistol_RoundsInMag")>0)
			{
				A_TakeInventory("TQ_9mm_Pistol_RoundsInMag",1);
				A_GiveInventory("TQ_9mm_Pistol_RoundInChamber",1);
				Return state("SlideForward");
			}
			return state("");
		}
	SlideLocked:
		PISG B 5
		Goto ReadyLoop
	SlideForward:
		PISG B 1
		PISG CDC 1 A_WeaponReady
		PISG AAAA 1 
		{
			A_WeaponReady;
			If(!(GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK))
			{ return state("ReadyLoop"); }
			CAllACS("TQ_RecoilOffset",(RecoilStrength/4)*(-1));
			return state("");
		}
		PISG A 0
		Goto ReadyLoop
		
	Flash:
		PISG F random(0,1)
		Stop
	
	FSight:
		TNT1 A 0
		{
			A_OverlayOffset(2,CAllACS("GetOffset",2)*(-0.75),CAllACS("GetOffset",3)*(-0.75));
			if ((CAllACS("GetOffset",2) > 30.0 || CAllACS("GetOffset",2) < -30.0 || CAllACS("GetOffset",3) > 30.0 || CAllACS("GetOffset",3) < -30.0))
			{ return state("HideFSight"); }
			Return state("");
		}
		SIGT A 1
		loop
	HideFSight:
		TNT1 A 1
		Goto FSight
	
	RSight:
		TNT1 A 0
		{
			A_OverlayOffset(3,CAllACS("GetOffset",2)*(-0.45),CAllACS("GetOffset",3)*(-0.45));
			if ((CAllACS("GetOffset",2) > 30.0 || CAllACS("GetOffset",2) < -30.0 || CAllACS("GetOffset",3) > 30.0 || CAllACS("GetOffset",3) < -30.0))
			{ return state("HideRSight"); }
			Return state("");
		}
		SIGT B 1
		loop
	HideRSight:
		TNT1 A 1
		Goto RSight
	}
}