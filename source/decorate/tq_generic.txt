//------------------------------------------------------------
//
// Generic counters, ammo, weapon definitions for inheritance
//
//------------------------------------------------------------

actor TQ_InventoryCounter : Inventory		// Modified from Vaecrius' Hideous Destructor
{
	+inventory.untossable
	inventory.maxamount 1
	inventory.amount 1
	+noclip
	height 0
	radius 0
	states
	{
	spawn:
		TNT1 A 0
		stop
	}
}

actor TQ_AimDownSights : TQ_InventoryCounter {}

actor TQ_Ammo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 100
	Inventory.Icon "CLIPA0"
	States
	{
	Spawn:
		CLIP A -1
		Stop
	}
}

actor TQ_Weapon : Weapon
{
	weapon.ammouse 1
	weapon.ammogive 20
	weapon.ammotype "TQ_Ammo"
	+WEAPON.DONTBOB
	States
	{
	Spawn:
		PIST A -1
		Stop
	Select:
		PISG A 1 A_Raise
		Loop
	Deselect:
		PISG A 1 A_Lower
		Loop
	Ready:
		PISG A 1
		{
			A_WeaponReady;
			if(CountInv("TQ_AimDownSights") > 0)
			{
				A_Overlay(2,"FSight");
				A_Overlay(3,"RSight");
				A_SetCrosshair(99);
			}
			else
			{
				A_SetCrosshair(98);
			}
		}
		Loop
	FSight:
		SIGT A 1 A_OverlayOffset(2,CAllACS("GetOffset",0)/(-360),CAllACS("GetOffset",1)/-(180))
		Stop
	RSight:
		SIGT B 1 A_OverlayOffset(3,CAllACS("GetOffset",0)/(720),CAllACS("GetOffset",1)/(360))
		Stop
	Fire:
		PISG C 5 A_FireBullets(CAllACS("GetOffset",0)/(180),CAllACS("GetOffset",1)/(-90),-1,1,"BulletPuff",FBF_EXPLICITANGLE|FBF_USEAMMO)
		Goto Ready
	AltFire:
		PISG B 5 {
			if(CountInv("TQ_AimDownSights") > 0)
			{
				A_TakeInventory("TQ_AimDownSights");
			}
			else
			{
				A_GiveInventory("TQ_AimDownSights");
			}
		}
		goto Ready
	}
}