//------------------------------------------------------------
//
// Generic counters, ammo, weapon definitions for inheritance
//
//------------------------------------------------------------

actor TQ_GenericCounter : Inventory		// Modified from Vaecrius' Hideous Destructor
{
	+inventory.untossable
	inventory.maxamount 1
	inventory.amount 1
	+noclip
	height 0
	radius 0
	states
	{
	spawn:
		TNT1 A 0
		stop
	}
}

actor TQ_AimDownSights : TQ_GenericCounter {}

Actor TQ_GenericBullet : FastProjectile
{
	Height 1
	Radius 1
	Speed 1
	Scale .1
	Decal "BulletChip"
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 Bright A_ChangeVelocity(frandom(-0.9,0.9),frandom(-0.9,0.9),-1)
		Loop
	Death:
		TNT1 A 1
		Stop
	XDeath:
		TNT1 A 1
		Stop
	}
}

Actor TQ_BulletPuff : BulletPuff
{
	radius 1
	height 1
	BounceType "Doom"
	RenderStyle Translucent
	Scale .75
	vspeed 0
	Alpha 0.75
	States
	{
	Spawn:
		TNT1 A 0
		PUFF CD random(1,2) A_ChangeVelocity(velx*.5,vely*.5,(velz*.5)-.1,CVF_REPLACE)
		TNT1 A 0 A_SetTranslucent(alpha*.5,0)
		PUFF DE random(2,4) A_ChangeVelocity(velx*.5,vely*.5,(velz*.5)-.1,CVF_REPLACE)
		TNT1 A 0 A_SetTranslucent(alpha*.5,0)
		PUFF EF random(4,8) A_ChangeVelocity(velx*.5,vely*.5,(velz*.5)-.1,CVF_REPLACE)
		TNT1 A 0 A_SetTranslucent(alpha*.5,0)
		PUFF FG random(8,16) A_ChangeVelocity(velx*.5,vely*.5,(velz*.5)-.1,CVF_REPLACE)
		TNT1 A 0 A_SetTranslucent(alpha*.5,0)
		PUFF GG random(16,32) A_ChangeVelocity(velx*.5,vely*.5,(velz*.5)-.1,CVF_REPLACE)
		Stop
	Melee:
		TNT1 A 0
		PUFF CD random(2,4) A_ChangeVelocity(.5,.5,-1)
		TNT1 A 0 A_SetTranslucent(alpha*.5,0)
		PUFF EF random(4,8) A_ChangeVelocity(.5,.5,-1)
		TNT1 A 0 A_SetTranslucent(alpha*.5,0)
		PUFF GG random(8,16) A_ChangeVelocity(.5,.5,-1)
		Stop
  }
}

actor TQ_GenericWeapon : Weapon
{
	Weapon.KickBack 100
	Weapon.BobRangeX 0.65
	Weapon.BobRangeY 0.5
	Weapon.BobSpeed 2.5
	Weapon.BobStyle InverseSmooth
	+WEAPON.NOAUTOFIRE
	+WEAPON.NO_AUTO_SWITCH
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	AltFire:
		"####" "#" 5
		{
			if(CountInv("TQ_AimDownSights") > 0)
			{
				A_TakeInventory("TQ_AimDownSights");
				A_SetCrosshair(98);
			}
			else
			{
				A_GiveInventory("TQ_AimDownSights");
				A_Overlay(2,"FSight");
				A_Overlay(3,"RSight");
				A_OverlayFlags(2,PSPF_ADDBOB,0);
				A_OverlayFlags(3,PSPF_ADDBOB,0);
				A_SetCrosshair(99);
			}
		}
		"####" "#" 0 { return state("Ready"); }
	FSight:
		SIGT A 1
		"####" "#" 1
		{
			A_OverlayOffset(2,CAllACS("GetOffset",0)*(-1),CAllACS("GetOffset",1)*(-1));
			if(CountInv("TQ_AimDownSights") > 0)
			{
				return state("FSight");
			}
			else
			{
				return state("StopSights");
			}
		}
		Stop
	RSight:
		SIGT B 1
		"####" "#" 1
		{
			A_OverlayOffset(3,CAllACS("GetOffset",0)*2,CAllACS("GetOffset",1));
			if(CountInv("TQ_AimDownSights") > 0)
			{
				return state("RSight");
			}
			else
			{
				return state("StopSights");
			}
		}
		Stop
	StopSights:
		TNT1 A 1
		Stop
	}
}