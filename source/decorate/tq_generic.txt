//------------------------------------------------------------
//
// Generic counters, ammo, weapon definitions for inheritance
//
//------------------------------------------------------------

actor TQ_GenericCounter : Inventory		// Modified from Vaecrius' Hideous Destructor
{
	+inventory.untossable
	inventory.maxamount 1
	inventory.amount 1
	+noclip
	height 0
	radius 0
	states
	{
	spawn:
		TNT1 A 0
		stop
	}
}

actor TQ_AimDownSights : TQ_GenericCounter {}

actor TQ_GenericAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 100
	Inventory.Icon "CLIPA0"
	States
	{
	Spawn:
		CLIP A -1
		Stop
	}
}

Actor TQ_GenericBullet : FastProjectile
{
	Height 1
	Radius 1
	Speed 1
	Scale .1
	Decal "BulletChip"
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 Bright A_ChangeVelocity(frandom(-0.9,0.9),frandom(-0.9,0.9),-1)
		Loop
	Death:
		TNT1 A 1
		Stop
	XDeath:
		TNT1 A 1
		Stop
	}
}

actor TQ_GenericWeapon : Weapon
{
	Weapon.KickBack 100
	Weapon.BobRangeX 0.45
	Weapon.BobRangeY 0.5
	Weapon.BobSpeed 2.5
	Weapon.BobStyle InverseSmooth
	+WEAPON.NOAUTOFIRE
	+WEAPON.NO_AUTO_SWITCH
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	FSight:
		SIGT A 1
		{
			A_OverlayOffset(2,CAllACS("GetOffset",0)/(-360),CAllACS("GetOffset",1)/-(180));
			if(CountInv("TQ_AimDownSights") > 0)
			{
				return state("FSight");
			}
			else
			{
				return state("");
			}
		}
		Stop
	RSight:
		SIGT B 1 
		{
			A_OverlayOffset(3,CAllACS("GetOffset",0)/(720),CAllACS("GetOffset",1)/(360));
			if(CountInv("TQ_AimDownSights") > 0)
			{
				return state("RSight");
			}
			else
			{
				return state("");
			}
		}
		Stop
	AltFire:
		"####" "#" 5
		{
			if(CountInv("TQ_AimDownSights") > 0)
			{
				A_TakeInventory("TQ_AimDownSights");
				A_SetCrosshair(98);
			}
			else
			{
				A_GiveInventory("TQ_AimDownSights");
				A_Overlay(2,"FSight");
				A_Overlay(3,"RSight");
				A_SetCrosshair(99);
			}
		}
		Goto Ready
	}
}