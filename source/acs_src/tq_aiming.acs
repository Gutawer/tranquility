#library "TQ_Aiming"
#include "zcommon.acs"

//------------------------------------------------------------
//
// Manage aim offsets
//
//------------------------------------------------------------


//Begin implementing eye, head, weapon, offsets
script "TQ_Tracking" enter
{
	int EyeYaw, EyePitch;
	int OffsetHeadYaw, OffsetHeadPitch;
	int OffsetWeaponYaw, OffsetWeaponPitch;
	while(1)
	{		
		//Get instant offsets for player looking
		EyeYaw=GetPlayerInput(-1,MODINPUT_YAW);
		EyePitch=GetPlayerInput(-1,MODINPUT_PITCH);
		
		//Subtract offsets from current head and weapon offsets,
		//so they "stay still" by comparison
		OffsetHeadYaw=GetUserVariable(0,"user_offsetheadyaw")-EyeYaw;
		OffsetHeadPitch=GetUserVariable(0,"user_offsetheadpitch")-EyePitch;
		
		OffsetWeaponYaw=GetUserVariable(0,"user_offsetweaponyaw")-EyeYaw;
		OffsetWeaponPitch=GetUserVariable(0,"user_offsetweaponpitch")-EyePitch;
		
		//Print yaw angles only for debugging
		print(f:EyeYaw,s:"\n",
			f:OffsetHeadYaw,s:"\n",
			f:OffsetWeaponYaw,s:"\n");
		
		SetUserVariable(0,"user_offsetheadyaw",OffsetHeadYaw);
		SetUserVariable(0,"user_offsetheadpitch",OffsetHeadPitch);
		
		SetUserVariable(0,"user_offsetweaponyaw",OffsetWeaponYaw);
		SetUserVariable(0,"user_offsetweaponpitch",OffsetWeaponPitch);
		
		delay(1);
	}
}

//Return offsets to zero 
script "TQ_OffsetReturn" enter
{
	int OffsetWeaponYaw, OffsetWeaponPitch;
	int OffsetWeaponYawVel, OffsetWeaponPitchVel;
	int OffsetHeadYaw, OffsetHeadPitch;
	int VelFactor = 0.025;
	int VelDecay = 0.85;
	
	while (1)
	{
		//Return weapon offsets by velocity to give "spring" effect
		OffsetWeaponYaw = GetUserVariable(0,"user_offsetweaponyaw");
		OffsetWeaponPitch = GetUserVariable(0,"user_offsetweaponpitch");
		
		OffsetWeaponYawVel = OffsetWeaponYawVel+(fixedmul(OffsetWeaponYaw,VelFactor));
		OffsetWeaponPitchVel = OffsetWeaponPitchVel+(fixedmul(OffsetWeaponPitch,VelFactor));
		
		OffsetWeaponYaw=OffsetWeaponYaw-OffsetWeaponYawVel;
		OffsetWeaponPitch=OffsetWeaponPitch-OffsetWeaponPitchVel;
		
		OffsetWeaponYawVel=(fixedmul(OffsetWeaponYawVel,VelDecay));
		OffsetWeaponPitchVel=(fixedmul(OffsetWeaponPitchVel,VelDecay));
		
		SetUserVariable(0,"user_offsetweaponyaw",OffsetWeaponYaw);
		SetUserVariable(0,"user_offsetweaponpitch",OffsetWeaponPitch);
		
		//Return head offsets smoothly, no spring
		OffsetHeadYaw=fixedmul(GetUserVariable(0,"user_offsetheadyaw"),0.5);
		OffsetHeadPitch=fixedmul(GetUserVariable(0,"user_offsetheadpitch"),0.5);
		
		SetUserVariable(0,"user_offsetheadyaw",OffsetHeadYaw);
		SetUserVariable(0,"user_offsetheadpitch",OffsetHeadPitch);
		
		delay(1);
	}
}