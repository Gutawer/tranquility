class TQ_Pistol : Weapon {
	enum BulletState {
		empty,
		live,
		fired,
	};

	bool SlideLocked;
	bool SafetyOn;
	bool MagIn;
	int RoundsInMag;
	BulletState Chamber;
	double SlidePosition;

	Default	{
		Weapon.SlotNumber 2;
	}

	override void PostBeginPlay() {
		Chamber = live;
		RoundsInMag = 7;
		MagIn = true;
		super.PostBeginPlay();
	}

	States {
		Spawn:
			PISG P -1;
			Loop;
		Select:
			TNT1 A 0 A_Print("Select");
			TNT1 A 1 A_Raise;
			Loop;
		Deselect:
			TNT1 A 0 A_Print("Deselect");
			TNT1 A 1 A_Lower;
			Loop;
		Ready:
			TNT1 A 1 TQ_InputChecker();
			Loop;
		Fire:
			TNT1 A 1 PullTrigger();
			Goto Ready;
		Reload:
			TNT1 A 5 MagRelease();
			TNT1 A 15 MagicReload();
			TNT1 A 5 MagInsert();
			Goto Ready;
		AltFire:
			TNT1 A 1 A_Print("PullSlide",0.5);
			TNT1 A 5 PullSlide();
			Goto Ready;
	}

	//Credit to SanyaWaffles via Discord
	action bool TQ_CheckButtonPress(int btn) {
		return ((GetPlayerInput(INPUT_BUTTONS) & btn) && !(GetPlayerInput(INPUT_OLDBUTTONS) & btn));
	}

	action state TQ_InputChecker() {
		if(TQ_CheckButtonPress(BT_ATTACK)) {
			return ResolveState("Fire");
		}
		if(TQ_CheckButtonPress(BT_RELOAD)) {
			return ResolveState("Reload");
		}
		if(TQ_CheckButtonPress(BT_ALTATTACK)) {
			return ResolveState("AltFire");
		}
		A_WeaponReady(WRF_NOFIRE|WRF_NOPRIMARY|WRF_NOSECONDARY);
		return ResolveState(null);
	}

	action void PullTrigger() {
		if(invoker.Chamber==live) {
			FireBullet();
		}
	}

	action void PullSlide() {
		A_PlaySound("TQ9mm_SlidePull",CHAN_AUTO);
		SlideBack();
	}

	action void FireBullet() {
		A_Print("Bang!",0.1);
		A_PlaySound("weapons/pistol",CHAN_AUTO);
		invoker.Chamber=fired;
		A_PlaySound("TQ9mm_SlideFast",CHAN_AUTO);
		SlideBack();
	}

	action void SlideBack() {
		EjectRound();
		SlideForward();
	}

	action void EjectRound() {
		invoker.Chamber=empty;
	}

	action void SlideForward() {
		A_PlaySound("TQ9mm_SlideSnap",CHAN_AUTO);
		if(invoker.MagIn==true && invoker.RoundsInMag>0) {
			invoker.RoundsInMag--;
			invoker.Chamber=live;
		}
	}

	action void MagRelease() {
		A_Print("MagOut",0.5);
		A_PlaySound("TQMagDrop",CHAN_AUTO);
		invoker.MagIn=false;
	}

	action void MagicReload() {
		A_Print("Magic Reload",0.5);
		invoker.RoundsInMag=7;
	}

	action void MagInsert() {
		A_Print("MagIn",0.5);
		A_PlaySound("TQMagIn",CHAN_AUTO);
		invoker.MagIn=true;
	}
}

//------------------------------------------------------------
//
// Player Definition
//
//------------------------------------------------------------
class TQ_Player : Doomplayer
{
	Default {
		Player.StartItem "TQ_Pistol";
		Player.StartItem "Fist";
		Player.Viewheight 50;
		Player.AttackZOffset 26;
		Player.ForwardMove 1.0;
		Player.SideMove 0.8;
		Player.RunHealth 35;
		Player.JumpZ 5.0;
	}
}